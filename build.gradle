plugins {
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'jacoco'
}

group = 'com.avilapps'
version = '0.0.1-SNAPSHOT'
description = 'pdf-services'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.11.799'
    implementation 'org.apache.pdfbox:pdfbox:2.0.19'

    implementation 'org.mapstruct:mapstruct:1.4.1.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.1.Final'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        html.enabled true
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/ServletInitializer.java',
                    '**/PdfServicesApplication.java',
                    '**/api/delegate/DocumentDelegate.java',
                    '**/api/mapper/**',
                    '**/api/model/**',
                    '**/common/**',
                    '**/domain/services/DocumentService.java',
                    '**/domain/model/**',
                    '**/domain/repository/FileRepository.java'
            ])
        }))
    }
}

check.dependsOn jacocoTestCoverageVerification

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
